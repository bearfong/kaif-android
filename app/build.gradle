plugins {
  id "me.tatarka.retrolambda" version "3.3.1"
}

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

android {

  def Properties versionProps = new Properties()
  versionProps.load(new FileInputStream(file('version.properties')))
  def buildVersionCode = versionProps['VERSION_CODE']
  def buildVersionName = versionProps['VERSION_NAME_PREFIX'] + "." + buildVersionCode;

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  packagingOptions {
    exclude 'LICENSE.txt'
    exclude 'META-INF/services/javax.annotation.processing.Processor'
  }

  compileSdkVersion 25
  buildToolsVersion rootProject.buildToolsVersion

  defaultConfig {
    applicationId "io.kaif.mobile"
    minSdkVersion 16
    targetSdkVersion 25
    versionCode buildVersionCode.toInteger()
    versionName buildVersionName
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }

  signingConfigs {
    def Properties props = new Properties()
    props.load(new FileInputStream(file('../secret/password.properties')))

    debug {
      storeFile file("../secret/kaif-keystore.jks")
      storePassword props["KEY_STORE_PASSWORD"]
    }
    release {
      storeFile file("../secret/kaif-keystore.jks")
      storePassword props["KEY_STORE_PASSWORD"]
      keyAlias "ReleaseKey"
      keyPassword props["RELEASE_KEY_PASSWORD"]
    }
  }

  buildTypes {
    def Properties props = new Properties()
    props.load(new FileInputStream(file('../secret/api.properties')))

    debug {
      signingConfig signingConfigs.debug
      buildConfigField 'String', 'CLIENT_ID', "\"" + props["CLIENT_ID"] + "\""
      buildConfigField 'String', 'CLIENT_SECRET', "\"" + props["CLIENT_SECRET"] + "\""
    }
    release {
      signingConfig signingConfigs.release
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      buildConfigField 'String', 'CLIENT_ID', "\"" + props["CLIENT_ID"] + "\""
      buildConfigField 'String', 'CLIENT_SECRET', "\"" + props["CLIENT_SECRET"] + "\""
    }
  }

  lintOptions {
    disable 'InvalidPackage'
    abortOnError false
  }

  testOptions.unitTests.all {
    testLogging {
      events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
    }
  }

  android.applicationVariants.all { variant ->
    variant.outputs.each { output ->
      def outputFile = output.outputFile
      if (outputFile != null && outputFile.name.endsWith('.apk')) {
        File apk = outputFile
        output.outputFile = new File(apk.parentFile, apk.name.replace(".apk", "-${buildVersionName}.apk"))
      }
    }
  }
}

dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])

  apt "com.google.dagger:dagger-compiler:" + rootProject.daggerVersion
  apt 'com.jakewharton:butterknife-compiler:' + rootProject.butterknifeVersion
  apt project(':retry-stale-processor')

  provided 'javax.annotation:jsr250-api:1.0'

  compile 'com.android.support:appcompat-v7:' + rootProject.supportLibVersion
  compile 'com.android.support:recyclerview-v7:' + rootProject.supportLibVersion
  compile 'com.android.support:design:' + rootProject.supportLibVersion
  compile 'com.android.support:support-v4:' + rootProject.supportLibVersion

  compile 'io.reactivex:rxandroid:1.2.0'
  compile 'com.jakewharton.rxbinding:rxbinding:0.4.0'
  compile 'com.jakewharton.rxbinding:rxbinding-design:0.4.0'
  compile 'com.trello:rxlifecycle:0.6.1'
  compile 'com.trello:rxlifecycle-components:0.6.1'

  compile 'com.jakewharton:butterknife:' + rootProject.butterknifeVersion
  compile 'com.squareup.retrofit2:retrofit:' + rootProject.retrofitVersion
  compile 'com.squareup.retrofit2:converter-gson:' + rootProject.retrofitVersion
  compile 'com.squareup.retrofit2:adapter-rxjava:' + rootProject.retrofitVersion
  compile 'com.google.dagger:dagger:' + rootProject.daggerVersion
  compile 'com.squareup.okhttp3:logging-interceptor:3.4.1'
  compile 'org.apache.commons:commons-lang3:3.4'

  debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
  releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'

  androidTestCompile "org.mockito:mockito-core:1.9.5"
  androidTestCompile "com.google.dexmaker:dexmaker:1.0"
  androidTestCompile "com.google.dexmaker:dexmaker-mockito:1.0"
  androidTestCompile 'com.android.support:support-annotations:' + rootProject.supportLibVersion
  androidTestCompile 'com.android.support.test:runner: ' + rootProject.runnerVersion
  androidTestCompile 'com.android.support.test:rules:' + rootProject.rulesVersion
  androidTestCompile 'com.android.support.test.espresso:espresso-core:' + rootProject.espressoVersion
  androidTestCompile 'com.android.support.test.espresso:espresso-intents:' + rootProject.espressoVersion

  androidTestCompile 'com.google.dagger:dagger:' + rootProject.daggerVersion
  androidTestApt 'com.google.dagger:dagger-compiler:' + rootProject.daggerVersion
}

task releaseNewVersion(dependsOn: 'assembleRelease') << {
  def Properties versionProps = new Properties()

  def file = file('version.properties')
  versionProps.load(new FileInputStream(file))

  def nextCode = versionProps['VERSION_CODE'].toInteger() + 1;
  versionProps['VERSION_CODE'] = nextCode.toString()
  versionProps.store(file.newWriter(), null)
}
